
## java字节码解读
### 1.源码
```
package day01;

public class Hello {
    public static void main(String[] args) {
        int base = 5;
        int[] nums = {1,3,5,7,9};
        int sum = 0;
        for (int num: nums) {
            if (num > base) sum += num;
        }
        System.out.println("大于5的总和："+sum);
    }
}
### 2.编译及反编译字节码
```
使用下面两个命令先后得到详细的字节码文件
```
 javac -g Hello.java -- 得到class字节码
--  javac 工具默认开启了优化功能, 生成的字节码中没有局部变量表 (LocalVariableTable)，
-- 相当于局部变量名称被擦除。如果需要这些调试信息, 在编 译时请加上 ‐g 选项。
 javap -c -verbose Hello.class
```
### 3.反编译字节码清单
字节码清单
```
  Last modified 2021-1-9; size 809 bytes
  MD5 checksum 854171da5bc6e8f99c05fc09b1429acd
  Compiled from "Hello.java"
public class day01.Hello
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #11.#23        // java/lang/Object."<init>":()V
   #2 = Fieldref           #24.#25        // java/lang/System.out:Ljava/io/PrintStream;
   #3 = Class              #26            // java/lang/StringBuilder
   #4 = Methodref          #3.#23         // java/lang/StringBuilder."<init>":()V
   #5 = String             #27            // 大于5的总和：
   #6 = Methodref          #3.#28         // java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
   #7 = Methodref          #3.#29         // java/lang/StringBuilder.append:(I)Ljava/lang/StringBuilder;
   #8 = Methodref          #3.#30         // java/lang/StringBuilder.toString:()Ljava/lang/String;
   #9 = Methodref          #31.#32        // java/io/PrintStream.println:(Ljava/lang/String;)V
  #10 = Class              #33            // day01/Hello
  #11 = Class              #34            // java/lang/Object
  #12 = Utf8               <init>
  #13 = Utf8               ()V
  #14 = Utf8               Code
  #15 = Utf8               LineNumberTable
  #16 = Utf8               main
  #17 = Utf8               ([Ljava/lang/String;)V
  #18 = Utf8               StackMapTable
  #19 = Class              #35            // "[Ljava/lang/String;"
  #20 = Class              #36            // "[I"
  #21 = Utf8               SourceFile
  #22 = Utf8               Hello.java
  #23 = NameAndType        #12:#13        // "<init>":()V
  #24 = Class              #37            // java/lang/System
  #25 = NameAndType        #38:#39        // out:Ljava/io/PrintStream;
  #26 = Utf8               java/lang/StringBuilder
  #27 = Utf8               大于5的总和：
  #28 = NameAndType        #40:#41        // append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
  #29 = NameAndType        #40:#42        // append:(I)Ljava/lang/StringBuilder;
  #30 = NameAndType        #43:#44        // toString:()Ljava/lang/String;
  #31 = Class              #45            // java/io/PrintStream
  #32 = NameAndType        #46:#47        // println:(Ljava/lang/String;)V
  #33 = Utf8               day01/Hello
  #34 = Utf8               java/lang/Object
  #35 = Utf8               [Ljava/lang/String;
  #36 = Utf8               [I
  #37 = Utf8               java/lang/System
  #38 = Utf8               out
  #39 = Utf8               Ljava/io/PrintStream;
  #40 = Utf8               append
  #41 = Utf8               (Ljava/lang/String;)Ljava/lang/StringBuilder;
  #42 = Utf8               (I)Ljava/lang/StringBuilder;
  #43 = Utf8               toString
  #44 = Utf8               ()Ljava/lang/String;
  #45 = Utf8               java/io/PrintStream
  #46 = Utf8               println
  #47 = Utf8               (Ljava/lang/String;)V
{
  public day01.Hello();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=8, args_size=1
         0: iconst_5
         1: istore_1
         2: iconst_5
         3: newarray       int
         5: dup
         6: iconst_0
         7: iconst_1
         8: iastore
         9: dup
        10: iconst_1
        11: iconst_3
        12: iastore
        13: dup
        14: iconst_2
        15: iconst_5
        16: iastore
        17: dup
        18: iconst_3
        19: bipush        7
        21: iastore
        22: dup
        23: iconst_4
        24: bipush        9
        26: iastore
        27: astore_2
        28: iconst_0
        29: istore_3
        30: aload_2
        31: astore        4
        33: aload         4
        35: arraylength
        36: istore        5
        38: iconst_0
        39: istore        6
        41: iload         6
        43: iload         5
        45: if_icmpge     72
        48: aload         4
        50: iload         6
        52: iaload
        53: istore        7
        55: iload         7
        57: iload_1
        58: if_icmple     66
        61: iload_3
        62: iload         7
        64: iadd
        65: istore_3
        66: iinc          6, 1
        69: goto          41
        72: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        75: new           #3                  // class java/lang/StringBuilder
        78: dup
        79: invokespecial #4                  // Method java/lang/StringBuilder."<init>":()V
        82: ldc           #5                  // String 大于5的总和：
        84: invokevirtual #6                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        87: iload_3
        88: invokevirtual #7                  // Method java/lang/StringBuilder.append:(I)Ljava/lang/StringBuilder;
        91: invokevirtual #8                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        94: invokevirtual #9                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        97: return
      LineNumberTable:
        line 5: 0
        line 6: 2
        line 7: 28
        line 8: 30
        line 9: 55
        line 8: 66
        line 11: 72
        line 12: 97
      StackMapTable: number_of_entries = 3
        frame_type = 255 /* full_frame */
          offset_delta = 41
          locals = [ class "[Ljava/lang/String;", int, class "[I", int, class "[I", int, int ]
          stack = []
        frame_type = 24 /* same */
        frame_type = 248 /* chop */
          offset_delta = 5
}
SourceFile: "Hello.java"

```

### 4.解读字节码清单
字节码中有几部分：
#### 4.1. 字节码基本信息
```
  Last modified 2021-1-9; size 966 bytes
  MD5 checksum 3f56d6d3d86429df9e4348e3a08f0efc
  Compiled from "Hello.java"
public class day01.Hello
  minor version: 0   -- 次版本0
  major version: 52  -- 主版本1.8
  flags: ACC_PUBLIC, ACC_SUPER  -- ACC_PUBLIC，这个类是public类，ACC_SUPER，修正invokespecial 指令调用 super 类方法的问题
```
#### 4.2. 常量池结构体
常量池中主要存储两类常量：字面量和符号引用。 
  - 字面量：文本字符串，final常量。 
  - 符号引用：类和接口的全局限定名。字段名称和描述符，方法的名称和描述符。

&emsp;&emsp;&emsp;&emsp;在jvm规范中，每个变量/字段都有描述信息，描述信息主要作用是描述字段的数据类型，方法的参数列表(包括数量、类型、顺序)与返回值。根据描述符规则，基本数据类型和代表无返回值的void类型都用一个大写字符来表示，对象类型则使用字符串L加对象的权限定名来表示。

&nbsp;&nbsp;&nbsp;&nbsp;对于数组类型来讲，每一个维度使用一个前置的[来表示，如int[]被标记为[I，String[][]被标记为[[Ljava.lang.String 注意：这个是给机器看的
  用描述符描述方法时，按照先参数列表，后返回值的顺序来描述。参数列表按照参数的严格顺序放在一组()之内。例如String getRealnameByIdAndNickname(int id, String name)的描述为:(I，Ljava/lang/string;) Ljava/lang/String;

&nbsp;&nbsp;&nbsp;&nbsp;摘录的部分常量池信息如下：
```
Constant pool:
   #1 = Methodref          #11.#34        // java/lang/Object."<init>":()V
   #2 = Fieldref           #35.#36        // java/lang/System.out:Ljava/io/PrintStream;
   #3 = Class              #37            // java/lang/StringBuilder
   #4 = Methodref          #3.#34         // java/lang/StringBuilder."<init>":()V
   #5 = String             #38            // 大于5的总和：
   #6 = Methodref          #3.#39         // java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
   #7 = Methodref          #3.#40         // java/lang/StringBuilder.append:(I)Ljava/lang/StringBuilder;
   #8 = Methodref          #3.#41         // java/lang/StringBuilder.toString:()Ljava/lang/String;
   #9 = Methodref          #42.#43        // java/io/PrintStream.println:(Ljava/lang/String;)V
  #10 = Class              #44            // day01/Hello
  #11 = Class              #45            // java/lang/Object
  ...
```
##### 4.2.1. 解读#6
```
#6 = Methodref          #3.#39         // java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
解读：
#6  常量池编号，文件中的其它地方可以引用
=   分隔符
Methodref   方法引用
#3.#39  引用了编号3和39的常量
Methodref   表示指向一个方法，StringBuilder类的append方法，方法入参是一个String对象，返回值是StringBuilder对象

```
#### 4.2.2. 构造函数和main方法
```
public day01.Hello();       
    descriptor: ()V         
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lday01/Hello;
```
```
stack=1, locals=1, args_size=1, 这个方法需要的栈深度为1，本地局部变量表的槽位1，方法参数1（指的是this）。
flags: ACC_PUBLIC   表示是一个public类
LineNumberTable:
line 3: 0   Java源码的行号与字节码指令的对应关系，在java类中是第3行，在字节码中是第0行

```

```
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return

aload_0，  将第一个引用类型变量，即this推送至栈顶，
invokespecial，  调用父类object的无参构造方法
```
```
LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lday01/Hello;

方法的局部变量描述，从位置0开始，长度位5，名称是this，签名是Lday01/Hello
方法体中的字节码解读
同上，
```
main方法解读
```
 public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=8, args_size=1
         0: iconst_5                # 将常量5，压入栈顶
         1: istore_1                # 将栈顶元素存储在槽位1
         2: iconst_5                # 将常量5压入栈顶
         3: newarray       int      # 结合编号2，初始化一个大小为5数组
         5: dup                     # 复制栈顶元素
         6: iconst_0                # 将常量0，压入栈顶
         7: iconst_1                # 将常量1，压入栈顶
         8: iastore                 # 将栈顶int型数值（1）存入指定数组的指定索引位置（0）
         9: dup
        10: iconst_1                # 编号10-27，依次将3、5、7、9存入数组中
        11: iconst_3
        12: iastore
        13: dup
        14: iconst_2
        15: iconst_5
        16: iastore
        17: dup
        18: iconst_3
        19: bipush        7
        21: iastore
        22: dup
        23: iconst_4
        24: bipush        9
        26: iastore
        27: astore_2
        28: iconst_0                # 编号28、29，初始化sum
        29: istore_3    
        30: aload_2                 # 编号30-31，将槽位2的数组压入栈顶，然后取出存放在槽位4
        31: astore        4
        33: aload         4         # 将槽位4的数组，压入栈顶
        35: arraylength             # 编号35-36，得出数组的长度，并存放在槽位5
        36: istore        5 
        38: iconst_0                # 编号38-39，将常量0压入栈顶，然后存入槽位6，作为计数器
        39: istore        6         
        41: iload         6         # 编号41-43，取出槽位6的计数器，和槽位5的数组长度作比较，如果大于后者，则跳出循环，进入编号72
        43: iload         5
        45: if_icmpge     72
        48: aload         4         # 编号48-66，将数组元素跟base值比较，若小于后者，进入编号66
        50: iload         6
        52: iaload
        53: istore        7
        55: iload         7
        57: iload_1
        58: if_icmple     66
        61: iload_3                 # 将sum值和满足条件的num想家
        62: iload         7
        64: iadd
        65: istore_3
        66: iinc          6, 1      # 将计数器累加1
        69: goto          41        # 跳到编号41，执行数组中下一个元素
        72: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        75: new           #3                  // class java/lang/StringBuilder
        78: dup
        79: invokespecial #4                  // Method java/lang/StringBuilder."<init>":()V
        82: ldc           #5                  // String 大于5的总和：
        84: invokevirtual #6                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        87: iload_3
        88: invokevirtual #7                  // Method java/lang/StringBuilder.append:(I)Ljava/lang/StringBuilder;
        91: invokevirtual #8                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        94: invokevirtual #9                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        97: return
         ...
         
```
